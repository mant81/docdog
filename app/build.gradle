plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    namespace "sschoi.docdog.viewer"
    compileSdk 34

    defaultConfig {
        applicationId "sschoi.docdog.viewer"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        File propertiesFile = rootProject.file("local.properties")
        if (propertiesFile.exists()) {
            properties.load(propertiesFile.newDataInputStream())
        }

        buildConfigField "String", "SUPABASE_URL", "\"${properties.getProperty('supabase.url', '')}\""
        buildConfigField "String", "SUPABASE_KEY", "\"${properties.getProperty('supabase.key', '')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/INDEX.LIST'
            excludes += 'META-INF/DEPENDENCIES'
        }
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Material Design
    implementation "com.google.android.material:material:1.11.0"

    // Supabase
    implementation platform("io.github.jan-tennert.supabase:bom:3.0.1")
    implementation "io.github.jan-tennert.supabase:postgrest-kt"
    implementation "io.github.jan-tennert.supabase:storage-kt"
    implementation "io.github.jan-tennert.supabase:auth-kt"
    
    // Kotlin Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3"

    // Ktor
    implementation "io.ktor:ktor-client-android:3.0.1"
    implementation "io.ktor:ktor-client-core:3.0.1"

    // WebView
    implementation "androidx.webkit:webkit:1.8.0"

    // Common AndroidX
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.browser:browser:1.8.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
